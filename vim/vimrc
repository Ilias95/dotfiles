""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Maintainer:
"       Ilias Stamatis <stamatis.iliass@gmail.com>
"
" Initially based on:
"       https://github.com/spf13/spf13-vim
"       https://bitbucket.org/pmav99/dotfiles/src/fdd48164c029/vim/vimrc
"
" Sections:
"       -> Setup Environment
"       -> Bundles
"       -> General
"       -> Basic Stuff
"       -> Indentation, Tabs, Spaces
"       -> Whitespace
"       -> Line Number
"       -> Scrolling and Folding
"       -> Wrap, Vertical Line
"       -> Buffers, Windows, Tabs
"       -> TimeOuts
"       -> Highlight and Searching
"       -> Backup and Swap
"       -> Shell Commands Output
"       -> Compile and Run Code
"       -> Languages
"       -> Misc
"       -> Plugins
"
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Setup Environment {
    " The following two lines are required by Vundle.
    set nocompatible                " be iMproved (must be first line)
    filetype off                    " required!

    " Setup Vundle.
    set rtp+=~/.vim/bundle/vundle
    call vundle#rc()
" }

" Bundles {
    " Dependencies {
        Bundle 'gmarik/vundle'
    " }

    " General {
        Bundle 'scrooloose/nerdtree'
        Bundle 'jistr/vim-nerdtree-tabs'
        Bundle 'spf13/vim-autoclose'
        Bundle 'mbbill/undotree'
    " }

    " General Programming {
        Bundle 'Shougo/neocomplcache'
        Bundle 'msanders/snipmate.vim'
        Bundle 'scrooloose/nerdcommenter'
        if executable('ctags')
            Bundle 'majutsushi/tagbar'
        endif
    " }
" }

" General {
    filetype plugin indent on       " automatically detect file types
    syntax on                       " syntax highlighting
    set synmaxcol=120               " don't try to highlight lines longer than 120 chars
    set mouse-=a                    " automatically disable mouse usage
    scriptencoding utf-8
    set encoding=utf-8
    set laststatus=2                " always show a status line
    set showmode                    " display the current mode
    set showcmd                     " show partial commands in status line and
    set visualbell
    set ttyfast
    set ruler                       " show the ruler
    set rulerformat=%30(%=\:b%n%y%m%r%w\ %l,%c%V\ %P%)
    set backspace=indent,eol,start  " backspace for dummies
    set history=1000                " store a ton of history (default is 20)
    set shell=$SHELL                " use user's default shell
    set fillchars=diff:⣿,vert:│
    set autowrite                   " autosave files
    set title

    colorscheme darkblue            " load colorscheme
    set background=dark
" }

" Basic Stuff {
    " With a map leader it's possible to do extra key combinations.
    let mapleader = ","
    let maplocalleader = '\\'

    " Map ; to :.
    " Saves typing and eliminates :Q style typos due to lazy holding shift.
    nnoremap ; :

    nnoremap <leader>w :w!<cr>
    nnoremap 0 ^

    " Use jk and ξκ (for greek layout) to exit insert/select mode.
    " <Esc> is inconvinient as it requires stretching to the top of the keyboard.
    inoremap jk <Esc>
    inoremap ξκ <Esc>
    snoremap jk <Esc>

    " <Del> key is too far...
    inoremap <leader>d <Del>

    " Use s (insert before) or S (insert after) to insert a single character.
    " Returns to normal mode automatically. It allows repetitions as well.
    function! RepeatChar(char, count)
        return repeat(a:char, a:count)
    endfunction
    nnoremap s :<C-U>exec "normal i".RepeatChar(nr2char(getchar()), v:count1)<cr>
    nnoremap S :<C-U>exec "normal a".RepeatChar(nr2char(getchar()), v:count1)<cr>

    " When vimrc is edited, reload it.
    autocmd! bufwritepost vimrc source ~/.dotfiles/vim/vimrc
" }

" Indentation, Tabs, Spaces {
    set tabstop=8                   " show tabs as 8 spaces
    set shiftwidth=8                " use indents of 8 spaces
    set shiftround                  " always round to multiple of 'shiftwidth'
    set softtabstop=8               " let backspace delete indent
    set noexpandtab                 " tabs are tabs, not spaces
    set smarttab
    set formatoptions=qrn1
    set autoindent                  " indent at the same level of the previous line

    autocmd FileType python,html,css,asm call g:ToggleIndentation(4)
    autocmd FileType python set expandtab  " tabs are spaces

    function! g:ToggleIndentation(num)
	execute 'set tabstop='.a:num
	execute 'set shiftwidth='.a:num
	execute 'set softtabstop='.a:num
    endfunc
" }

" Whitespace {
    " Remove trailing whitespaces when saving.
    autocmd BufWritePre * :%s/\s\+$//e

    " Insert empty line above or below while staying in normal mode.
    nnoremap <leader>o :set paste<cr>m`o<Esc>``:set nopaste<cr>
    nnoremap <leader>O :set paste<cr>m`O<Esc>``:set nopaste<cr>
" }

" Line Numbers {
    set number                      " in case relative line numbers are not supported
    set relativenumber              " relative line numbers on

    " Switch between relative and absolute line number.
    function! g:ToggleNuMode()
        if(&relativenumber == 1)
            set number
        else
            set relativenumber
        endif
    endfunc

    nnoremap <leader>nn :call g:ToggleNuMode()<cr>
" }

" Scrolling and Folding {
    set scrolloff=3                 " minimum lines to keep above and below cursor
    set foldenable                  " auto fold code

    " Map space to toggle the current fold open/closed.
    nnoremap <space> za

    " Code folding options.
    nnoremap <leader>f0 :set foldlevel=0<cr>
    nnoremap <leader>f1 :set foldlevel=1<cr>
    nnoremap <leader>f2 :set foldlevel=2<cr>
    nnoremap <leader>f3 :set foldlevel=3<cr>
    nnoremap <leader>f4 :set foldlevel=4<cr>
    nnoremap <leader>f5 :set foldlevel=5<cr>
    nnoremap <leader>f6 :set foldlevel=6<cr>
    nnoremap <leader>f7 :set foldlevel=7<cr>
    nnoremap <leader>f8 :set foldlevel=8<cr>
    nnoremap <leader>f9 :set foldlevel=9<cr>
" }

" Wrap, Vertical Line {
    set wrap                      " wrap long lines
    set whichwrap=b,s,h,l,<,>,[,]   " backspace and cursor keys wrap to

    " Wrapped lines goes down/up to next row, rather than next line in file.
    nnoremap j gj
    nnoremap k gk

    autocmd FileType c,cpp,java,python,javascript set colorcolumn=80

    " Show/hide vertical line.
    function! g:ToggleColorColumn()
        if(&colorcolumn == 0)
            set colorcolumn=80
        else
            set colorcolumn=0
        endif
    endfunc

    nnoremap <leader>l :call g:ToggleColorColumn()<cr>
" }

" Buffers, Windows, Tabs {
    set hidden                      " allow buffer switching without saving
    set winminheight=0              " windows can be 0 line high
    set splitbelow                  " when splitting put new window below of the current one
    set splitright                  " when splitting put new window to the right of the current one
    au VimResized * :wincmd =       " resize splits when the windows is resized.
    au FocusLost * : silent! wall   " save when losing focus

    " Redraw screen.
    nnoremap <leader>u :syntax sync fromstart<cr>:redraw!<cr>

    " Fast close window.
    nnoremap <leader>q :q<cr>

    " Easier moving in windows.
    nnoremap <C-J> <C-W>j<C-W>_
    nnoremap <C-K> <C-W>k<C-W>_
    nnoremap <C-L> <C-W>l<C-W>_
    nnoremap <C-H> <C-W>h<C-W>_

    " Adjust viewports to the same size.
    nnoremap <Leader>= <C-w>=

    " Useful mappings for managing tabs.
    nnoremap <leader>tn :tabnew<cr>
    nnoremap <leader>to :tabonly<cr>
    nnoremap <leader>tc :tabclose<cr>
    nnoremap <leader>tm :tabmove
    nnoremap > gt
    nnoremap < gT

    " Jump to the desired tab immediately.
    function! MoveToTab(tabposition)
        tabfirst
        for i in range(a:tabposition)
            tabnext
        endfor
    endfunction

    nnoremap <leader>t0 :call MoveToTab(0)<cr>
    nnoremap <leader>t1 :call MoveToTab(1)<cr>
    nnoremap <leader>t2 :call MoveToTab(2)<cr>
    nnoremap <leader>t3 :call MoveToTab(3)<cr>
    nnoremap <leader>t4 :call MoveToTab(4)<cr>
    nnoremap <leader>t5 :call MoveToTab(5)<cr>
    nnoremap <leader>t6 :call MoveToTab(6)<cr>
    nnoremap <leader>t7 :call MoveToTab(7)<cr>
    nnoremap <leader>t8 :call MoveToTab(8)<cr>
    nnoremap <leader>t9 :call MoveToTab(9)<cr>
" }

" TimeOuts {
    " Time out on key codes but not mappings.
    " Basically this makes terminal Vim work sanely.
    set notimeout
    set ttimeout
    set ttimeoutlen=10
" }

" Highlight and Searching {
    set incsearch                   " incremental search
    set hlsearch                    " highlight search terms
    set ignorecase                  " case insensitive search
    set smartcase                   " case sensitive when uc present

    " Use sane regexes.
    nnoremap / /\v
    vnoremap / /\v

    " Matching pairs.
    set showmatch                   " show matching symbols (parens etc)
    set matchtime=3                 " time to show matching paren
    set matchpairs+=(:)
    set matchpairs+=[:]
    set matchpairs+={:}
    set matchpairs+=<:>

    " Clear highlighted search.
    nnoremap <silent> <leader>/ :nohlsearch<cr>

    " Keep search matches in the middle of the window.
    nnoremap n nzzzv

    " Highlight word occurrences but do not jump to the next result.
    nnoremap <leader>* *N
" }

" Backup and Swap {
    " Backup {
        let directory = $HOME . "/.vim/backup/"
        execute "set backupdir=".directory
        set backup                  " enable backups

        " Create backup directory if it doesn't exists.
        if !isdirectory(directory)
            execute "!mkdir -p ".directory
        endif
    " }

    " Swap {
        set noswapfile              " turn swap off
    " }
" }

" Shell Commands Output {
    " Display output of shell commands in new window.
    " Only one window by command, if a window already exists for a command, it will be reused.
    " E.g. :Shell ls
    function! s:ExecuteInShell(command, bang)
        let _ = a:bang != '' ? s:_ : a:command == '' ? '' : join(map(split(a:command), 'expand(v:val)'))

        if (_ != '')
            let s:_ = _
            let bufnr = bufnr('%')
            let winnr = bufwinnr('^' . _ . '$')
            silent! execute  winnr < 0 ? 'belowright new ' . fnameescape(_) : winnr . 'wincmd w'
            setlocal buftype=nowrite bufhidden=wipe nobuflisted noswapfile wrap number
            silent! :%d
            let message = 'Execute ' . _ . '...'
            call append(0, message)
            echo message
            silent! 2d | resize 1 | redraw
            silent! execute 'silent! %!'. _
            silent! execute 'resize ' . line('$')
            silent! execute 'syntax on'
            silent! execute 'autocmd BufUnload <buffer> execute bufwinnr(' . bufnr . ') . ''wincmd w'''
            silent! execute 'autocmd BufEnter <buffer> execute ''resize '' .  line(''$'')'
            silent! execute 'nnoremap <silent> <buffer> <cr> :call <SID>ExecuteInShell(''' . _ . ''', '''')<cr>'
            " Reexecute command with <localleader>r in a window opened by :Shell.
            silent! execute 'nnoremap <silent> <buffer> <LocalLeader>r :call <SID>ExecuteInShell(''' . _ . ''', '''')<cr>'
            " Go to the previous window with <localleader>g.
            silent! execute 'nnoremap <silent> <buffer> <LocalLeader>g :execute bufwinnr(' . bufnr . ') . ''wincmd w''<cr>'
            nnoremap <silent> <buffer> <C-W>_ :execute 'resize ' . line('$')<cr>
            silent! syntax on
        endif
    endfunction

    command! -complete=shellcmd -nargs=* -bang Shell call s:ExecuteInShell(<q-args>, '<bang>')
" }

" Compile and Run Code {
    " run
    autocmd FileType python nnoremap <leader>r :w<cr> :!clear; ./%<cr>
    autocmd FileType c,cpp nnoremap <leader>r :!clear; ./%:r<cr>
    autocmd FileType java nnoremap <leader>r :!clear; java %:r<cr>

    " compile
    autocmd FileType c,cpp,java nnoremap <leader>m :w<cr> :Shell clear; make<cr>
    autocmd FileType c nnoremap <leader>c8 :Shell gcc -Wall -W -pedantic -ansi -std=c89 -o %:r %<cr>
    autocmd FileType c nnoremap <leader>c9 :Shell gcc -Wall -W -pedantic -ansi -std=c99 -o %:r %<cr>
    autocmd FileType cpp nnoremap <leader>c1 :Shell g++ -std=c++11 -o %:r %<cr>
    autocmd FileType java nnoremap <leader>c9 :Shell javac %<cr>

    " Make file executable and reload it.
    nnoremap <leader>x :!chmod +x %<cr> :e %<cr>
" }

" Languages {
    " Greek {
        " Use Greek letters in command mode.
        :set langmap=ΑA,ΒB,ΨC,ΔD,ΕE,ΦF,ΓG,ΗH,ΙI,ΞJ,ΚK,ΛL,ΜM,ΝN,ΟO,ΠP,QQ,ΡR,ΣS,ΤT,ΘU,ΩV,WW,ΧX,ΥY,ΖZ,αa,βb,ψc,δd,εe,φf,γg,ηh,ιi,ξj,κk,λl,μm,νn,οo,πp,qq,ρr,σs,τt,θu,ωv,ςw,χx,υy,ζz
    " }
" }

" Misc {
    " Clipboard {
        if has ('x') && has ('gui')
            " Use + register for copy-paste.
            set clipboard=unnamedplus
        endif

        nnoremap <leader>p :set paste<cr>"+p<cr>:set nopaste<cr>
        nnoremap <leader>P :set paste<cr>"+P<cr>:set nopaste<cr>
        nnoremap <leader>y "+y
        vnoremap <leader>y "+ygv

        " Yank from the cursor to the end of the line, to be consistent with C and D.
        nnoremap Y y$
    " }

    " Automaticaly close C comments.
    inoremap /* /*  */<Left><Left><Left>

    " Make sure Vim returns to the same line when you reopen a file.
    augroup line_return
        au!
        au BufReadPost *
            \ if line("'\"") > 0 && line("'\"") <= line("$") |
            \     execute 'normal! g`"zvzz' |
            \ endif
    augroup END

    " For when you forget to sudo.. Really Write the file.
    cnoremap w!! w !sudo tee % >/dev/null
" }

" Plugins {
    " Ctags {
        set tags=./tags;/,~/.vimtags
    " }

    " neocomplcache {
        let g:acp_enableAtStartup = 0
        let g:neocomplcache_enable_at_startup = 1
        let g:neocomplcache_enable_camel_case_completion = 1
        let g:neocomplcache_enable_smart_case = 1
        let g:neocomplcache_enable_underbar_completion = 1
        let g:neocomplcache_enable_auto_delimiter = 1
        let g:neocomplcache_max_list = 15
        let g:neocomplcache_force_overwrite_completefunc = 1

        " Define dictionary.
        let g:neocomplcache_dictionary_filetype_lists = {
            \ 'default' : '',
            \ 'vimshell' : $HOME.'/.vimshell_hist',
            \ 'scheme' : $HOME.'/.gosh_completions'
            \ }

        " Define keyword.
        if !exists('g:neocomplcache_keyword_patterns')
            let g:neocomplcache_keyword_patterns = {}
        endif
        let g:neocomplcache_keyword_patterns._ = '\h\w*'

        " Plugin key-mappings.
        inoremap <expr><C-g> neocomplcache#undo_completion()
        inoremap <expr><C-l> neocomplcache#complete_common_string()
        inoremap <expr><cr> neocomplcache#complete_common_string()
        " <TAB>: completion.
        inoremap <expr><TAB> pumvisible() ? "\<C-n>" : "\<TAB>"
        inoremap <expr><S-TAB> pumvisible() ? "\<C-p>" : "\<TAB>"
        " <cr>: close popup
        " <s-cr>: close popup and save indent.
        inoremap <expr><s-cr> pumvisible() ? neocomplcache#close_popup()"\<cr>" : "\<cr>"
        inoremap <expr><cr> pumvisible() ? neocomplcache#close_popup() : "\<cr>"
        " <C-h>, <BS>: close popup and delete backword char.
        inoremap <expr><BS> neocomplcache#smart_close_popup()."\<C-h>"
        inoremap <expr><C-y> neocomplcache#close_popup()

        " Enable omni completion.
        autocmd FileType css setlocal omnifunc=csscomplete#CompleteCSS
        autocmd FileType html,markdown setlocal omnifunc=htmlcomplete#CompleteTags
        autocmd FileType javascript setlocal omnifunc=javascriptcomplete#CompleteJS
        autocmd FileType python setlocal omnifunc=pythoncomplete#Complete
        autocmd FileType xml setlocal omnifunc=xmlcomplete#CompleteTags
        autocmd FileType ruby setlocal omnifunc=rubycomplete#Complete
        " Enable heavy omni completion.
        if !exists('g:neocomplcache_omni_patterns')
            let g:neocomplcache_omni_patterns = {}
        endif
        let g:neocomplcache_omni_patterns.php = '[^. \t]->\h\w*\|\h\w*::'
        let g:neocomplcache_omni_patterns.perl = '\h\w*->\h\w*\|\h\w*::'
        let g:neocomplcache_omni_patterns.c = '[^.[:digit:] *\t]\%(\.\|->\)'
        let g:neocomplcache_omni_patterns.cpp = '[^.[:digit:] *\t]\%(\.\|->\)\|\h\w*::'
        let g:neocomplcache_omni_patterns.ruby = '[^. *\t]\.\h\w*\|\h\w*::'
    " }

    " NerdTree - NerdTreeTabs {
        nnoremap <C-e> :NERDTreeTabsToggle<cr>
        nnoremap <leader>e :NERDTreeFind<cr>
        nnoremap <leader>nt :NERDTreeFind<cr>

        let NERDTreeShowBookmarks=1
        let NERDTreeIgnore=['\.pyc', '\~$', '\.swo$', '\.swp$', '\.git', '\.hg', '\.svn', '\.bzr']
        let NERDTreeChDirMode=0
        let NERDTreeQuitOnOpen=1
        let NERDTreeMouseMode=2
        let NERDTreeShowHidden=1
        let NERDTreeKeepTreeInNewTab=1
        let g:nerdtree_tabs_open_on_gui_startup=0
    " }

    " TagBar {
         nnoremap <silent> <leader>tt :TagbarToggle<cr>
    " }

    " undotree {
        " If undotree is opened, it is likely one wants to interact with it.
        let g:undotree_setfocuswhentoggle=1
        nnoremap <leader>u :UndotreeToggle<cr>
    " }

    " misc {
        let g:nerdshutup=1
        let b:match_ignorecase = 1
    " }
" }

" Modeline {
" vim: set tabstop=4 shiftwidth=4 softtabstop=4 expandtab:
" }

